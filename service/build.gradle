plugins {
	id 'idea'
	id 'java'

	id 'org.springframework.boot' version '2.5.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'

	id 'com.github.ben-manes.versions' version '0.39.0'
	id 'com.diffplug.spotless' version '5.12.5'
	id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'bio.terra'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

def artifactGroup = "${group}.${rootProject.getName()}"

configurations {
	compileOnly { extendsFrom annotationProcessor }
}

repositories {
	mavenCentral()
	maven { url "https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/" }
	maven { url "https://broadinstitute.jfrog.io/broadinstitute/libs-release/" }
}

dependencies {
	// Spring deps
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Terra deps
	implementation 'bio.terra:terra-common-lib:0.0.46-SNAPSHOT'
	implementation 'org.broadinstitute.dsde.workbench:sam-client_2.13:0.1-beb5b1c'

	// Swagger deps
	implementation 'io.swagger.core.v3:swagger-annotations:2.1.9'
	runtimeOnly 'org.webjars.npm:swagger-ui-dist:3.50.0'
	swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.26'
	// Used for Swagger but versioned by Spring:
	implementation 'javax.validation:validation-api'
	implementation 'org.webjars:webjars-locator-core'

	// DB deps (versioned by Spring)
	implementation 'org.liquibase:liquibase-core'
	runtimeOnly 'org.postgresql:postgresql'
}

swaggerSources {
	service {
		inputFile = file('../common/openapi.yml')
		code {
			language = 'spring'
			components = ['models', 'apis']
			additionalProperties = [
					modelPackage     : "${artifactGroup}.generated.model",
					apiPackage       : "${artifactGroup}.generated.api",
					dateLibrary      : 'java8',
					interfaceOnly    : true,
					useTags          : true,
					springBootVersion: dependencyManagement.managedVersions['org.springframework.boot:spring-boot']
			] as Map
		}
	}
}

idea {
	module {
		generatedSourceDirs = [file("${swaggerSources.service.code.outputDir}/src/main/java")]
		downloadJavadoc = true
	}
}

test { useJUnitPlatform() }

spotless {
	java {
		googleJavaFormat()
		target 'src/*/java/**/*.java'
	}
}

task bootRunDev {
	bootRun.configure { systemProperty 'spring.profiles.active', 'human-readable-logging' }
	finalizedBy(bootRun)
}

sourceSets.main.java.srcDir "${swaggerSources.service.code.outputDir}/src/main/java"

compileJava.dependsOn swaggerSources.service.code, spotlessApply

