dependencies {
	implementation 'io.swagger.core.v3:swagger-annotations'
	runtimeOnly 'org.webjars.npm:swagger-ui-dist:3.51.2'
	swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli'

	// Versioned by Spring:
	implementation 'javax.validation:validation-api'
	implementation 'org.webjars:webjars-locator-core'
}

generateSwaggerCode {
	inputFile = file('../common/openapi.yml')
	language = 'spring'
	components = ['models', 'apis']
	additionalProperties = [
			modelPackage     : "${artifactGroup}.generated.model",
			apiPackage       : "${artifactGroup}.generated.api",
			dateLibrary      : 'java8',
			interfaceOnly    : 'true',
			useTags          : 'true',
			springBootVersion: dependencyManagement.managedVersions['org.springframework.boot:spring-boot']
	]
	rawOptions = ['--import-mappings', "VersionProperties=${artifactGroup}.config.VersionProperties"]
}

idea.module.generatedSourceDirs = [file("${generateSwaggerCode.outputDir}/src/main/java")]
sourceSets.main.java.srcDir "${generateSwaggerCode.outputDir}/src/main/java"
compileJava.dependsOn generateSwaggerCode


// see https://github.com/n0mer/gradle-git-properties
gitProperties {
	gitPropertiesName = 'rendered/version.properties'
	keys = []
	customProperty('externalcreds.version.gitTag', { it.describe(tags: true) })
	customProperty('externalcreds.version.gitHash', { it.head().abbreviatedId })
	customProperty('externalcreds.version.github', { "https://github.com/DataBiosphere/terra-external-credentials-manager/tree/${it.describe(tags: true)}" })
	customProperty('externalcreds.version.build', version)
}
